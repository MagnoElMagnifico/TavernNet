plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.0-M3'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'tavernnet'
version = '0.0.1-SNAPSHOT'
description = 'TavernNet Social Network'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

// Mostrar todos los warnings
allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:all" << "-Xlint:-serial" << "-Werror"
        }
        tasks.withType(Javadoc) {
            options.addStringOption('Xwerror', '-quiet')
        }
    }
}

repositories {
    mavenCentral()
    // Para poder usar el SNAPSHOT
    maven { url = 'https://repo.spring.io/snapshot' }
}

// Simplificar estructura de carpetas
sourceSets {
    main.java.srcDirs = ['src']
    main.resources.srcDirs = ['resources']
    test.java.srcDirs = ['test']
}

dependencies {
    // Aplicaciones REST usando Spring MVC. Usa Tomcat embebido como servidor
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Bases de datos: MongoDB y Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // Anotaciones de validación de código
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation 'com.fasterxml.jackson.core:jackson-databind'

    // Live reloading tras compilar
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    // developmentOnly 'org.springframework.boot:spring-boot-docker-compose'

    // Autenticacion y autorizacion
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'io.jsonwebtoken:jjwt-api:0.13.0'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.13.0'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.13.0'
}
